// Build.gradle for creating or installing new instrumentation modules

// Global defaults - override here or in individual modules as needed.
project.ext {
  nrJavaAgentVersion = '6.0.0'
  group = 'com.newrelic.instrumentation'
}

buildscript {
  repositories {
    maven {
      url 'https://dl.bintray.com/cjstehno/public'
      metadataSources {
        mavenPom()
        artifact()
      }
    }
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'gradle-templates:gradle-templates:1.5'
    classpath 'com.newrelic.agent.java:gradle-verify-instrumentation-plugin:3.2'
  }
}

import templates.*

task createModule {
  description = 'Generate project files for a new instrumentation module'
  group = 'New Relic'
  doLast {
    String projectGroup = TemplatesPlugin.prompt('Instrumentation Module Group (default: ' + project.ext.group + '):\n')
    String projectName = TemplatesPlugin.prompt('Instrumentation Module Name:\n')

    if (projectName == null) {
      throw new Exception("Please specify a valid module name.")
    } else {
      projectName = projectName.trim()
    }

    if (projectGroup == null || projectGroup.trim() == '') {
      projectGroup = project.ext.group
    } else {
      projectGroup = projectGroup.trim()
    }

    def projectPath = new File(projectName)
    if (projectPath.exists()) {
      throw new Exception(projectPath.path + ' already exists.')
    }

    def projectJava = new File(projectPath, 'src/main/java')
    def projectTest = new File(projectPath, 'src/test/java')
    mkdir projectJava
    mkdir projectTest

    ProjectTemplate.fromRoot(projectPath) {
       'build.gradle' '''
        // Build.gradle generated for instrumentation module PROJECT_NAME

        apply plugin: 'java'

        dependencies {
           // Declare a dependency on each JAR you want to instrument
           // Example:
           // implementation 'javax.servlet:servlet-api:2.5'

           // New Relic Java Agent dependencies
           implementation 'com.newrelic.agent.java:newrelic-agent:NR_AGENT_VERSION'
           implementation 'com.newrelic.agent.java:newrelic-api:NR_AGENT_VERSION'
        }

        jar {
          manifest {
            attributes 'Implementation-Title': 'PROJECT_GROUP.PROJECT_NAME'
            attributes 'Implementation-Vendor': 'New Relic'
            attributes 'Implementation-Vendor-Id': 'com.newrelic'
            attributes 'Implementation-Version': 1.0
          }
        }

        verifyInstrumentation {
          // Verifier plugin documentation:
          // https://github.com/newrelic/newrelic-gradle-verify-instrumentation
          // Example:
          // passes 'javax.servlet:servlet-api:[2.2,2.5]'
          // exclude 'javax.servlet:servlet-api:2.4.public_draft'
        }'''.replace('PROJECT_GROUP', projectGroup)
            .replace('PROJECT_NAME', projectName)
            .replace('PROJECT_PATH', projectPath.path)
            .replace('NR_AGENT_VERSION', project.ext.nrJavaAgentVersion)
     }

     File settings = new File('settings.gradle')
     settings.append("include '$projectName'\n")
     println "Created module in $projectPath.path."
  }
}

subprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'com.newrelic.gradle-verify-instrumentation-plugin'

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.11'
    testImplementation 'com.newrelic.agent.java:instrumentation-test:' + project.nrJavaAgentVersion
    testImplementation 'com.newrelic.agent.java:newrelic-agent:' + project.nrJavaAgentVersion
  }

  task install(dependsOn: jar, type: Copy) {
    description = 'Copies compiled jar to the NEW_RELIC_EXTENSIONS_DIR.'
    group  = 'New Relic'

    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR") ?: " "

    from jar
    into extDir
  }

  install.doFirst  {
    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR")
     if (extDir == null) {
         throw new Exception("Must set NEW_RELIC_EXTENSIONS_DIR.")
     }

     if (extDir.startsWith("~" + File.separator)) {
         extDir = System.getProperty("user.home") + extDir.substring(1);
     }

     if (!file(extDir).directory) {
         throw new Exception(extDir + "NEW_RELIC_EXTENSIONS_DIR, set as '" + extDir + "'is not a valid directory.")
     }
  }
}
